use v6;

unit module RakuPlay;

use YAMLish;

sub queue-build ( %params ) is export {

  my $os = %params<os>;
  my $code = %params<code>;
  my $code-description = %params<description>;
  my $skip-zef = %params<skip-zef>;
  my $with-profile = %params<with-profile>;
  my $modules = %params<modules>;
  my $rakudo_version = %params<rakudo_version> || "default";
  my $rakudo-version-mnemonic = %params<rakudo-version-mnemonic>;

  my $worker-num = (1 .. 3).pick(1);

  my $user = "rkp-{$worker-num}-{$rakudo_version.chop(32)}";

  my $description =  "RakuPlay: [$code-description]. Rakudo version: [$rakudo-version-mnemonic]. OS: [$os]";

  my $rnd = ('a' .. 'z').pick(20).join('');

  my $id = "{$rnd}{$*PID}";

  my $effective-dir = "{%*ENV<HOME>}/projects/RakuPlay/.cache/{$id}";

  mkdir "{$effective-dir}/files";

  copy "runners/default/sparrowfile", "{$effective-dir}/sparrowfile";

  shell "cp -r {%*ENV<PWD>}/issues $effective-dir/files";

  spurt "$effective-dir/files/code.raku", $code;

  spurt "$effective-dir/files/Rakufile", $modules if $modules;

  spurt "$effective-dir/config.pl6", "%(
    user => '$user',
    rakudo_version => '$rakudo_version',
    skip-zef => $skip-zef,
    with-profile => $with-profile,
);\n";



  my $sparky-dir = "{%*ENV<HOME>}/projects/RakuDist/sparky/RakuPlay-{$worker-num}";

  mkdir "{$sparky-dir}/.triggers/";

  spurt "{$sparky-dir}/sparrowfile", "# dummy file, generated by rakuplay";

  spurt "{$sparky-dir}/.triggers/$id", "%(
    cwd =>  '$effective-dir',
    sparrowdo => %( conf => 'config.pl6', docker => '{$os}-rakudist', bootstrap => False, no_index_update => False, no_sudo => True,   ),
    description => '$description',
);\n";

  say "queue build: {$sparky-dir}/.triggers/$id: ",
      "{$sparky-dir}/.triggers/$id".IO.slurp;

  return $id


}

sub get-webui-conf is export {

  my $conf-file = %*ENV<HOME> ~ '/rakuplay-web.yaml';

  my %conf = $conf-file.IO ~~ :f ?? load-yaml($conf-file.IO.slurp) !! Hash.new;

  warn "RakuPlay web conf loaded: ", $conf-file;

  %conf;

}

