use JSON::Tiny;

my $user = config()<user>;

my $rakudo-version = config()<rakudo_version>;

my $directory = $*CWD;

my $zef-debug = False; # could be overiden in custom sparrowfile

# Alpine patch

if os() eq 'alpine' {

  # this is needed for alpine rakudo installation
  unless "/bin/zef".IO ~~ :e {
    copy "/opt/rakudo-pkg/share/perl6/core/bin/zef", "/bin/zef"
  }

  # this is needed for alpine rakudo installation
  unless "/bin/perl6".IO ~~ :e {
    copy "/opt/rakudo-pkg/bin/perl6", "/bin/perl6"
  }

}

# ------------------------------------------- INSTALL Rakudo -------------------------------------------------------- #

say "<<< PREPARE Environment >>>";

if "/home/$user/rakuplay-{$rakudo-version}.ok".IO ~~ :f {

	say "rakuplay environment already set for rakudo version - [$rakudo-version]";

} else {

	user $user;

	module-run 'Rakudo::Install', %(
	  user => $user,
	  rakudo-version => $rakudo-version,
	  skip-install-dependencies => True,
    skip-zef => config()<skip-zef>,
	);

	bash Q|sed -i '2 i\($*OUT,$*ERR).map: {.out-buffer = 0}' $(which zef); echo $(which zef); cat $(which zef)|, %(
	  description => "zef patch for user $user",
	  user => $user
	);

	spurt "/home/$user/rakuplay-{$rakudo-version}.ok", ""; # we use file to notify that rakuplay env is set, so don't set it next time
}

# ------------------------------------------- INSTALL Modules -------------------------------------------------------- #

if "files/Rakufile".IO ~~ :f {
  say "*** install Raku modules from Rakufile file ***";
  # install Raku modules from Rakufile file
  for "files/Rakufile".IO.lines -> $line {
    next if $line ~~ /^^ \s* '#' /;
    next unless $line ~~ /\S/;
    my @params = $line.split(/\s+/);
    my $module = @params.shift;
    my $zef-options = $zef-debug ?? "--debug" !! "";
    $zef-options ~=  " {@params}" if @params;
    bash "zef install $module $zef-options", %(
      description => "zef install $module $zef-options",
      user => $user,
      debug => False
    )
  }
}

if "files/code.raku".IO ~~ :e {

  my $tm = "rakuplay-{time}";	

  copy  "files/code.raku", "/home/$user/code.{$tm}.raku";

  bash "perl6 --version ", %(
  	user => $user,
  	description => "raku version"
  );

  if config()<with-profile> {
     bash "timeout 100 perl6 --profile=code.{$tm}.raku.html -e 'say EVALFILE qq[code.{$tm}.raku]' 1>{$tm}.stdout 2>{$tm}.stderr && echo > {$tm}.ok; : ", %(
  	user => $user,
	description => "run raku code"
     );
  } else {
     bash "timeout 100 perl6 -e 'say EVALFILE qq[code.{$tm}.raku]' 1>{$tm}.stdout 2>{$tm}.stderr && echo > {$tm}.ok; : ", %(
  	user => $user,
	description => "run raku code"
     );
  }

  say "";
  say "executing code:";
  say "files/code.raku".IO.slurp;
  say "";

  if "/home/$user/{$tm}.stdout".IO ~~ :e {
   say "stdout:";
   say "/home/$user/{$tm}.stdout".IO.slurp;
   say "";
  }

  if "/home/$user/{$tm}.stderr".IO ~~ :e {
   say "stderr:";
   say "/home/$user/{$tm}.stderr".IO.slurp;
   say "";
  }

  unlink "/home/$user/{$tm}.stdout" if "/home/$user/{$tm}.stdout".IO ~~ :e;
  unlink "/home/$user/{$tm}.stderr" if "/home/$user/{$tm}.stderr".IO ~~ :e;

  unlink "/home/$user/code.{$tm}.raku" if "/home/$user/code.{$tm}.raku".IO ~~ :e;

	say "===";

	if  "/home/$user/{$tm}.ok".IO ~~ :e {
	 unlink  "/home/$user/{$tm}.ok";
	 say "RakuPlay: OK";

	} else {
	 say "RakuPlay: FAIL";
	 exit(1); 
	}

  if "/home/$user/code.{$tm}.raku.html".IO ~~ :e {
    say "\n===\nSending Raku profile data to public gist ...";
    my $data =  "/home/$user/code.{$tm}.raku.html".IO.slurp;
    my %data = %(
     description => "Raku profile data. Rakudo Version: $rakudo-version",
     public => True,
     files => {
       "data.html" => {
         "content" => $data
       }
     },
    );
    "/home/$user/code.{$tm}.raku.html.gist".IO.spurt(to-json(%data));
    #say "";
    my $token = "ea3824a247442ff19643ddcf7a73029a34e30eca";
    bash "ls -lh /home/$user/code.{$tm}.raku.html ; curl -s https://api.github.com/gists -d \@/home/$user/code.{$tm}.raku.html.gist -D - -H 'Accept: application/vnd.github.v3+json' -H 'Authorization: token $token'|grep html_url|head -n 1", %(
     description => "Publish Raku profile to github gist"
    );
    unlink "/home/$user/code.{$tm}.raku.html";
    unlink "/home/$user/code.{$tm}.raku.html.gist";
  }

} else {
  say "no code passed, exiting ...";
  exit(0);
}


